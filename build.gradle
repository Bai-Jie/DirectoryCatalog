List<String> javaSubprojects = [
        ':domain',
        ':business',
        ':gateway',
        ':client:cli',
        ':app'
]

javaSubprojects.each { projectPath ->
    project(projectPath) {
        apply plugin: 'java'
        apply plugin: 'checkstyle'
        apply plugin: 'pmd'
        apply plugin: 'findbugs'

        sourceCompatibility = JavaVersion.VERSION_1_7
        version = '2.0'

        group = 'gq.baijie.catalog'

        tasks.withType(JavaCompile) { task ->
            task.options.encoding = 'UTF-8'
        }
        tasks.withType(Javadoc) { task ->
            task.options.encoding = 'UTF-8'
        }

        repositories {
            jcenter()
        }

        dependencies {
            testCompile 'junit:junit:4.12'
            testCompile 'com.google.code.findbugs:annotations:3.0.0'
        }

        checkstyle {
            toolVersion = "6.2"
            configFile = rootProject.file 'config/checkstyle/checkstyle.xml'
        }

        pmd {
            ruleSets = ["java-basic", "java-braces"/*, "java-junit"*/]
        }

    }
}
